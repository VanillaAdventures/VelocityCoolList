plugins {
    id 'java'
    id 'eclipse'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.0.1"
    id "xyz.jpenilla.run-velocity" version "2.3.1"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.atikinbtw'
version = '3.1.0'

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'elytrium-repo'
        url = 'https://maven.elytrium.net/repo/'
    }
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:3.4.0-SNAPSHOT'
    annotationProcessor 'com.velocitypowered:velocity-api:3.4.0-SNAPSHOT'

    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'

    // LimboAPI dependency
    compileOnly 'net.elytrium.limboapi:api:1.1.26'

    implementation 'com.github.Carleslc.Simple-YAML:Simple-Yaml:1.8.4'
    
    // Discord JDA
    implementation 'net.dv8tion:JDA:5.0.0-beta.15'
    
    // Database dependencies
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'org.xerial:sqlite-jdbc:3.46.0.0'
    implementation 'com.mysql:mysql-connector-j:8.4.0'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

jar {
    finalizedBy("shadowJar")
    archiveFileName.set(project.name + "-" + archiveVersion.get() + "-original.jar")
}

shadowJar {
    archiveFileName.set(project.name + "-" + archiveVersion.get() + ".jar")

    // Classifier for publishing
    archiveClassifier.set("shaded")

    mustRunAfter("build")

    // Merge META-INF/services files where needed
    mergeServiceFiles()

    // Exclude signatures, maven/ and proguard/ from META-INF
    exclude("META-INF/*.SF")
    exclude("META-INF/*.DSA")
    exclude("META-INF/*.RSA")
    exclude("META-INF/maven/**")
    exclude("META-INF/proguard/**")

    dependencies {
        include(dependency("com.github.Carleslc.Simple-YAML:Simple-Yaml:1.8.4"))
        include(dependency("net.dv8tion:JDA:5.0.0-beta.15"))
        include(dependency("com.zaxxer:HikariCP:5.1.0"))
        include(dependency("org.xerial:sqlite-jdbc:3.46.0.0"))
        include(dependency("com.mysql:mysql-connector-j:8.4.0"))
    }
}

runVelocity {
    velocityVersion("3.4.0-SNAPSHOT")
}